<!DOCTYPE html>
<html>
  <head>
    <title>Funkcije</title>
    <meta charset="utf-8">
    <style>
      body {
        font-family: 'Avenir Next';
      }
      h3 {
        font-weight: normal;
      }
      .remark-code, .remark-inline-code {
        font-family: 'Menlo';
      }
      .remark-code {
        font-size: 1.5em;
      }
      ol, ul {
        font-size: 1.5em;
      }

      a.reveal {
        font-weight: bold;
      }

      a.reveal:hover {
        cursor:pointer;
      }

      .question {
        color: white;
        background-color: black;
      }

      .red {
        color: red;
      }

      .green {
        color: green;
      }

      .consecutive pre {
        margin-top: 0;
        margin-bottom: 0;
      }
    </style>
  </head>
  <body>
    <textarea id="source">

class: center, middle

# Programiranje 1

## Rekurzija in funkcije višjega reda

### 14. november 2016

---

class: center, middle

## Lastnosti Haskella


### statični tipi
### leno izvajanje
### čist jezik
### funkcijski jezik

---

### Tipi v Haskellu so **statični**

```haskell
λ> let jeMajhen x =
     if x < 10 then "Da" else False

<interactive>:96:52:
  Couldn't match expected type ‘[Char]’
    with actual type ‘Bool’
  In the expression:
    False
  In the expression:
    if x < 10 then "Da" else False
```

---

### Tipi v Pythonu so **dinamični**

```python
>>> def je_majhen(x):
...     return 'Da' if x < 10 else False
```

```python
>>> je_majhen(3) + '!'
'Da!'
```

```python
>>> je_majhen(10) + '!'
Traceback (most recent call last):
  File "<stdin>", line 1, in <module>
TypeError: unsupported operand type(s)
  for +: 'bool' and 'str'
```

---

### Tipi v Pythonu so **dinamični**

```python
>>> def fakulteta(n):
...     if n <= 1:
...         return 1
...     else:
...         n * fakulteta(n - 1)
```

```python
>>> fakulteta(4)
Traceback (most recent call last):
  File "<stdin>", line 1, in <module>
  File "<stdin>", line 5, in fakulteta
  File "<stdin>", line 5, in fakulteta
TypeError: unsupported operand type(s)
  for *: 'int' and 'NoneType'
```

---

### Python je **neučakan** jezik

```python
>>> def ignoriraj(x): return 0
>>> def vsota():
...     return sum(range(10 ** 8))
>>> def vsota2():
...     return ignoriraj(vsota())
```

```python
>>> from timeit import timeit

>>> timeit(vsota, number=1)
3.8711246129969368

>>> timeit(vsota2, number=1)
3.9942239959927974
```

---

### Haskell je **len** jezik

```haskell
λ> let ignoriraj x = 0
λ> let vsota = sum [1..10^6]
λ> let vsota' = ignoriraj vsota
```

```haskell
λ> :set +s

λ> vsota
500000500000
(0.73 secs, 175,876,008 bytes)

λ> vsota'
0
(0.00 secs, 1,039,616 bytes)
```

---

### Haskell je **len** jezik

```haskell
λ> [1,2..]
[1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,...

λ> take 10 [1,2..]
[1,2,3,4,5,6,7,8,9,10]

λ> "tro" ++ cycle "lo"
"trololololololololololololololololo...

λ> take 7 ("tro" ++ cycle "lo")
"trololo"

λ> [x | x <- [1,2..], 100 `mod` x == 0]
[1,2,4,5,10,20,25,50,100
```

---

### Haskell **je čist** jezik

1. Programi nimajo stranskih učinkov.

2. Program lahko vedno zamenjamo z njegovo definicijo.

3. Funkcije ob enakih argumentih vrnejo enake vrednosti.

### **Izjemno koristno** za dokazovanje.
### **Obvezno** za len jezik.
---

### Python **ni čist** jezik

```python
>>> seznam = [1]
>>> def f(x):
...     seznam.append(x)
...     return len(seznam)
>>> def g(x):
...     return f(x) + f(x)
```

```python
>>> g(3)
5
>>> f(3) + f(3)
9
>>> g(3)
13
```

---

### Haskell **je čist** jezik

1. Programi nimajo stranskih učinkov.

2. Program lahko vedno zamenjamo z njegovo definicijo.

3. Funkcije ob enakih argumentih vrnejo enake vrednosti.

### **Izjemno koristno** za dokazovanje.
### **Obvezno** za len jezik.

---

### Haskell je **funkcijski** jezik

### Programe pišemo s sestavljanjem funkcij.

### Python je **proceduralni\*** jezik

### Programe pišemo z zaporedjem ukazov.

---

class: center, middle

## Lastnosti Haskella


### statični tipi
### leno izvajanje
### čist jezik
### funkcijski jezik

---

class: center, middle

## Rekurzija

---

### Rekurzija je osnovno orodje Haskella

```haskell
fakulteta 0 = 1
fakulteta n = n * fakulteta (n - 1)
```

```haskell
dolzina [] = 0
dolzina (_:xs) = 1 + dolzina xs
```

```haskell
zadrga :: [a] -> [b] -> [(a, b)]
zadrga [] _ = []
zadrga _ [] = []
zadrga (x:xs) (y:ys) =
  (x,y) : zadrga xs ys
```
---

### Rekurzivne so lahko tudi druge vrednosti

```haskell
enDvaTri = 1 : 2 : 3 : enDvaTri
```

```haskell
splosniFib a b = a : splosniFib b (a + b)
fib = splosniFib 0 1
```

---

class: center, middle

## Funkcije višjega reda

---

### Kaj, če funkciji ne podamo vseh argumentov?

```haskell
zmnozi :: Int -> Int -> Int
zmnozi x y = x * y
```

--

```haskell
λ> zmnozi 2 3
6
```

--

```haskell
λ> zmnozi 2
<interactive>:3:1:
No instance for (Show (Int -> Int))
...
```

--

```haskell
λ> :t zmnozi 2
zmnozi 2 :: Int -> Int
```

---

### Kaj, če funkciji ne podamo vseh argumentov?

```haskell
λ> let podvoji = zmnozi 2

λ> podvoji 10
20

λ> podvoji 3
6
```

--

```haskell
λ> (zmnozi 2) 3
6
```

---

### Delno uporabljene funkcije

```haskell
λ> let ostevilci = zip [1..]

λ> ostevilci "abcde"
[(1,'a'),(2,'b'),(3,'c'),(4,'d'),(5,'e')]

λ> ostevilci [True, False, True]
[(1,True),(2,False),(3,True)]
```

```haskell
λ> let triGlave = take 3

λ> triGlave "Triglavska roža"
"Tri"
```

---

### Delno uporabljene infiksne funkcije

```haskell
λ> let razpolovi = (/ 2)

λ> razpolovi 3
1.5
```

```haskell
λ> let inverz = (1 /)

λ> inverz 7
0.14285714285714285
```

```haskell
λ> let zadnjaStevka = (`mod` 10)

λ> zadnjaStevka 12345
5
```


---

class: center

## Funkcija z več argumenti je v resnici funkcija enega argumenta, ki vrne funkcijo.

### Izraz **`f x y`** je enak **`(f x) y`**.

### Tip **`A -> B -> C`** je enak **`A -> (B -> C)`**.

---

class: center

## Funkcija z več argumenti je v resnici funkcija enega argumenta, ki vrne funkcijo.

### Izraz **`f x y z`** je enak .spoiler[**`((f x) y) z`**].

### Tip **`A->B->C->D`** je enak .spoiler[**`A->(B->(C->D))`**].

#### Aplikacija je .spoiler[levo] asociativna.

#### `->` je .spoiler[desno] asociativna.

---

### Funkcije lahko podamo kot argumente funkcij

```haskell
trikrat :: (a -> a) -> a -> a
trikrat f x = f (f (f x))
```

```haskell
λ> trikrat succ 39
42

λ> trikrat (+ 3) 5
14

λ> let mojaFunkcija x = 2 * x + 1

λ> trikrat mojaFunkcija 1
15
```

---

class: question

### Ali razumem, kako deluje `trikrat`?

<pre><code class="haskell hljs remark-code">λ> trikrat (trikrat succ) 1
<span class="spoiler">10</span>
</code></pre>

<pre><code class="haskell hljs remark-code">λ> (trikrat trikrat) succ 1
<span class="spoiler">28</span>
</code></pre>

---

### Neimenovane funkcije pišemo kot `\x -> ...`

```haskell
λ> let mojaFunkcija x = 2 * x + 1

λ> trikrat mojaFunkcija 1
15
```

```haskell
λ> trikrat (\x -> 2 * x + 1) 1
15
```

```haskell
λ> trikrat (\x -> 5 * x - 2) 1
63
```

```haskell
λ> let mojaFunkcija' =  \x -> 2 * x + 1
```

---
class: center

### Zgodovina lambde

--

### $$x \mapsto x + 3$$

--

### $$\hat{x} . x + 3$$

--

### $$\hat{} x . x + 3$$

--

### $$\lambda x . x + 3$$

--

### `\x -> x + 3`

---

class: center, middle 
## Osnovne funkcije za delo s funkcijami

---

### Funkcije uporabljamo z `$`

```haskell
($) :: (a -> b) -> a -> b
f $ x = f x
```

```haskell
  f $ g $ h $ x

= f $ (g $ (h $ x))

= f (g (h x))
```

---

### Funkcije komponiramo s `.`

```haskell
(.) :: (b -> c) -> (a -> b) -> a -> c
f . g = \x -> f (g x)
```

```haskell
λ> let f = succ . (* 3)

λ> f 13
40
```

```haskell
λ> let g = (* 3) . succ

λ> g 13
42
```

---

### Funkcije curryiramo s `curry`

```haskell
curry :: ((a, b) -> c) -> a -> b -> c
curry f = \x -> \y -> f (x, y)
```

```haskell
uncurry :: (a -> b -> c) -> (a, b) -> c
uncurry g = \(x, y) -> g x y
```
--

## $$C^{A \times B} \cong (C^B)^A$$

### $$curry \colon C^{A \times B} \to (C^B)^A$$
### $$uncurry \colon (C^B)^A \to C^{A \times B}$$

---

class: middle, center
## Osnovne funkcije za delo s seznami

---

### Z `map` funkcijo uporabimo po elementih

```haskell
λ> map (*5) [10, 20, 30]
[50,100,150]

λ> map length ["Jaz", "sem", "robot"]
[3,3,5]

λ> map (\n -> 3 * n + 1) [1..10]
[4,7,10,13,16,19,22,25,28,31]
```

<pre><code class="haskell hljs remark-code">map :: <span class="spoiler"><span class="spoiler">(a -> b)</span> -> <span class="spoiler">[a]</span> -> <span class="spoiler">[b]</span></span>
map f [] = <span class="spoiler">[]</span>
map f (x:xs) = <span class="spoiler">f x : map f xs</span>
</code></pre>



---

### Iz seznamov izbiramo s `filter`

```haskell
λ> filter even [1..10]
[2,4,6,8,10]

λ> filter (\n -> n `mod` 5 == 1) [1..30]
[1,6,11,16,21,26]

λ> filter (`elem` "aeiou") "abrakadabra"
"aaaaa"
```


<pre><code class="haskell hljs remark-code">filter :: <span class="spoiler"><span class="spoiler">(a -> Bool)</span> -> <span class="spoiler">[a]</span> -> <span class="spoiler">[a]</span></span>
filter p [] = <span class="spoiler">[]</span>
filter p (x:xs)
  | p x = <span class="spoiler">x : filter p xs</span>
  | otherwise = <span class="spoiler">filter p xs</span>
</code></pre>

---

### Ustrezen začetek izberemo s `takeWhile`

```haskell
λ> takeWhile odd [1,3,5,2,1,2,5]
[1,3,5]

λ> takeWhile (\n -> 6 `mod` n == 0) [1..]
[1,2,3]
```

<pre><code class="haskell hljs remark-code">takeWhile :: <span class="spoiler"><span class="spoiler">(a -> Bool)</span> -> <span class="spoiler">[a]</span> -> <span class="spoiler">[a]</span></span>
takeWhile p [] = <span class="spoiler">[]</span>
takeWhile p (x:xs)
  | p x = <span class="spoiler">x : takeWhile p xs</span>
  | otherwise = <span class="spoiler">[]</span>
</code></pre>

---

class: center, middle

## Zlaganje seznamov

---

class: middle

```haskell
  sum [1, 2, 3]
= 1 + sum [2, 3]
= 1 + (2 + sum [3])
= 1 + (2 + (3 + sum []))
= 1 + (2 + (3 + 0))
```

---

class: middle

```haskell
  product [1, 2, 3]
= 1 * product [2, 3]
= 1 * (2 * product [3])
= 1 * (2 * (3 * product []))
= 1 * (2 * (3 * 1))
```

---

class: middle

```haskell
  length [1, 2, 3]
= 1 + length [2, 3]
= 1 + (1 + length [3])
= 1 + (1 + (1 + length []))
= 1 + (1 + (1 + 0))
```

---

class: middle

```haskell
  reverse [1, 2, 3]
= reverse [2, 3] ++ [1]
= (reverse [3] ++ [2]) ++ [1]
= ((reverse [] ++ [3]) ++ [2]) ++ [1]
= (([] ++ [3]) ++ [2]) ++ [1]
```

---

class: middle

```haskell
  map f [1, 2, 3]
= f 1 : map f [2, 3]
= f 1 : (f 2 : map f [3])
= f 1 : (f 2 : (f 3 : map f []))
= f 1 : (f 2 : (f 3 : []))
```

---

class: middle

```haskell
  foldr f z [1, 2, 3]
= f 1 (foldr f z [2, 3])
= f 1 (f 2 (foldr f z [3]))
= f 1 (f 2 (f 3 (foldr f z [])))
= f 1 (f 2 (f 3 z))
```

--

```haskell
foldr f z [] = z
foldr f z (x:xs) = f x (foldr f z xs)
```

---

### Izrazimo `sum` s `foldr`

```haskell
  sum [1, 2, 3]
= 1 + sum [2, 3]
= 1 + (2 + sum [3])
= 1 + (2 + (3 + sum []))
= 1 + (2 + (3 + 0))
```

```haskell
  foldr f z [1, 2, 3]
= f 1 (foldr f z [2, 3])
= f 1 (f 2 (foldr f z [3]))
= f 1 (f 2 (f 3 (foldr f z [])))
= f 1 (f 2 (f 3 z))
```

<pre><code class="haskell hljs remark-code">sum xs = foldr <span class="spoiler">(\x acc -> x + acc)</span> <span class="spoiler">0</span> <span class="spoiler">xs</span>
</code></pre>

---

### Eta ekvivalenca

```haskell
sum xs = foldr (\x acc -> x + acc) 0 xs
```

--

```haskell
sum xs = foldr (+) 0 xs
```
--

```haskell
sum xs = ((foldr (+)) 0) xs
```

--

```haskell
sum = ((foldr (+)) 0)
```

--

```haskell
sum = foldr (+) 0
```

--

.center[```haskell
f x = (...) x
f = (...)
```]

---

### Izrazimo `product` s `foldr`

```haskell
  product [1, 2, 3]
= 1 * product [2, 3]
= 1 * (2 * product [3])
= 1 * (2 * (3 * product []))
= 1 * (2 * (3 * 1))
```

```haskell
  foldr f z [1, 2, 3]
= f 1 (foldr f z [2, 3])
= f 1 (f 2 (foldr f z [3]))
= f 1 (f 2 (f 3 (foldr f z [])))
= f 1 (f 2 (f 3 z))
```

<pre><code class="haskell hljs remark-code">product = foldr <span class="spoiler">(*)</span> <span class="spoiler">1</span>
</code></pre>

---

### Izrazimo `length` s `foldr`

```haskell
  length [1, 2, 3]
= 1 + length [2, 3]
= 1 + (1 + length [3])
= 1 + (1 + (1 + length []))
= 1 + (1 + (1 + 0))
```

```haskell
  foldr f z [1, 2, 3]
= f 1 (foldr f z [2, 3])
= f 1 (f 2 (foldr f z [3]))
= f 1 (f 2 (f 3 (foldr f z [])))
= f 1 (f 2 (f 3 z))
```

<pre><code class="haskell hljs remark-code">length = foldr <span class="spoiler">(\_ acc -> 1 + acc)</span> <span class="spoiler">0</span>
</code></pre>

---

### Izrazimo `reverse` s `foldr`

```haskell
  reverse [1, 2, 3]
= reverse [2, 3] ++ [1]
= (reverse [3] ++ [2]) ++ [1]
= ((reverse [] ++ [3]) ++ [2]) ++ [1]
= (([] ++ [3]) ++ [2]) ++ [1]
```

```haskell
  foldr f z [1, 2, 3]
= f 1 (foldr f z [2, 3])
= f 1 (f 2 (foldr f z [3]))
= f 1 (f 2 (f 3 (foldr f z [])))
= f 1 (f 2 (f 3 z))
```

<pre><code class="haskell hljs remark-code">reverse = foldr <span class="spoiler">(\x acc -> acc ++ [x])</span> <span class="spoiler">[]</span>
</code></pre>

---

### Izrazimo `map` s `foldr`

```haskell
  map g [1, 2, 3]
= g 1 : map g [2, 3]
= g 1 : (g 2 : map g [3])
= g 1 : (g 2 : (g 3 : map g []))
= g 1 : (g 2 : (g 3 : []))
```

```haskell
  foldr f z [1, 2, 3]
= f 1 (foldr f z [2, 3])
= f 1 (f 2 (foldr f z [3]))
= f 1 (f 2 (f 3 (foldr f z [])))
= f 1 (f 2 (f 3 z))
```

<pre><code class="haskell hljs remark-code">map g = foldr <span class="spoiler">(\x acc -> g x : acc)</span> <span class="spoiler">[]</span>
</code></pre>

---

### Neprazne sezname zlagamo s `foldr1`

```haskell
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr f z [x1, x2, x3]
  = f x1 (f x2 (f x3 z))
  = x1 `f` (x2 `f` (x3 `f` z))
```

```haskell
foldr1 :: (a -> a -> a) -> [a] -> a
foldr1 f [x1, x2, x3]
  = f x1 (f x2 x3)
  = x1 `f` (x2 `f` x3)
```

```haskell
maximum = foldr1 max
```

---

### Sezname s `foldl` zlagamo z leve

```haskell
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr f z [x1, x2, x3]
  = f x1 (f x2 (f x3 z))
  = x1 `f` (x2 `f` (x3 `f` z))
```

```haskell
foldl :: (b -> a -> b) -> b -> [a] -> b
foldl f z [x1, x2, x3]
  = f (f (f z x1) x2) x3
  = ((z `f` x1) `f` x2) `f` x3
```

```haskell
reverse = foldl (\acc x -> x : acc) []
```

---

class: center, middle, question

### [Project Euler #1](https://projecteuler.net/problem=1)
### [Project Euler #2](https://projecteuler.net/problem=2)
### [Project Euler #3](https://projecteuler.net/problem=3)
### [Project Euler #4](https://projecteuler.net/problem=4)
### [Project Euler #5](https://projecteuler.net/problem=5)
### [Project Euler #6](https://projecteuler.net/problem=6)
### [Project Euler #7](https://projecteuler.net/problem=7)

---

class: center, middle

### naslednjič...


# Definicije tipov


    </textarea>
    <script src="../../js/jquery-1.11.3.min.js"></script>
    <script src="../../js/remark-latest.min.js"></script>
    <script src="https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML"></script>
    <script>
      $(document).ready(function() { 
      
      $(".spoiler").hide();
      
      $('<a class="reveal">???</a>').insertBefore('.spoiler');

      $("a.reveal").click(function(){
        $(this).next().fadeIn(500);
        $(this).hide();
      });

    }); 
        var slideshow = remark.create({
        highlightLines: true
      });
    </script>
  </body>
</html>
