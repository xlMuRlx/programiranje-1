<!DOCTYPE html>
<html>
  <head>
    <title>Razredi tipov</title>
    <meta charset="utf-8">
    <style>
      body {
        font-family: 'Avenir Next';
      }
      h3 {
        font-weight: normal;
      }
      .remark-code, .remark-inline-code {
        font-family: 'Menlo';
      }
      .remark-code {
        font-size: 1.5em;
      }
      ol, ul {
        font-size: 1.5em;
      }

      a.reveal {
        font-weight: bold;
      }

      a.reveal:hover {
        cursor:pointer;
      }

      .question {
        /*color: white;*/
        /*background-color: black;*/
      }

      .red {
        color: red;
      }

      .green {
        color: green;
      }

      .consecutive pre {
        margin-top: 0;
        margin-bottom: 0;
      }
    </style>
  </head>
  <body>
    <textarea id="source">

class: center, middle

# Programiranje 1

### 28. november 2016

---

class: center, middle

## Razredi tipov

---

class: center, middle

### Vrednosti lahko definiramo za različne tipe.


---

### En tip, ena definicija.

```haskell
pobrisiSamoglasnike :: String -> String
pobrisiSamoglasnike =
    filter $ (`notElem` "aeiou")

odgovor :: Integer
odgovor = 42

aliJeHaskellZakon :: Bool
aliJeHaskellZakon = True
```

#### Take definicije dobro poznamo.

---

### Več tipov, ena definicija.

```haskell
map :: (a -> b) -> [a] -> [b]
map _ [] = []
map f (x:xs) = f x : map f xs

prazenSeznam :: [a]
prazenSeznam = []

($) :: (a -> b) -> a -> b
f $ x = f x
```

#### Takim definicijam pravimo, da so parametrično polimorfne.

---

### En tip, več definicij?

```haskell
odgovor :: Integer
odgovor = 42

odgovor :: Integer
odgovor = 24
```

#### Prihaja do zmede.

```haskell
test.hs:4:1:
  Duplicate type signatures for ‘odgovor’

test.hs:5:1:
  Multiple declarations of ‘odgovor’
```

---

### Več tipov, več definicij?

```haskell
najnaj :: Integer
najnaj = 42

najnaj :: Bool
najnaj = True
```

#### Takim definicijam pravimo, da so ad-hoc polimorfne.<br>Vendar v tej obliki iz istega razloga kot prej niso dovoljene.

```haskell
test.hs:4:1:
  Duplicate type signatures for ‘najnaj’

test.hs:5:1:
  Multiple declarations of ‘najnaj’
```

---

### Rešitev ponujajo razredi tipov.

```haskell
class  Osebni a  where
    najnaj :: a
```

#### Razred podamo s signaturo: imeni in tipi vrednosti

```haskell
instance  Osebni Integer  where
    najnaj = 42

instance  Osebni Bool  where
    najnaj = True
```

#### Člana razreda podamo z definicijo vrednosti iz signature.

---

### V signaturi lahko naštejemo več vrednosti.

```haskell
class  Osebni a  where
    najnaj :: a
    izboljsaj :: a -> a
```

```haskell
instance  Osebni Integer  where
    najnaj = 42
    izboljsaj n = n + 1

instance  Osebni Bool  where
    najnaj = True
    izboljsaj b = b || True
```

---

### Tipi razreda `Show` podpirajo prikaz.

```haskell
class  Show a  where
    show :: a -> String
```

```haskell
instance  Show Bool  where
    show True = "True"
    show False = "False"
```

---


### Tipi razreda `Eq` podpirajo enakost.

```haskell
class  Eq a  where
    (==) :: a -> a -> Bool
    (/=) :: a -> a -> Bool
```

```haskell
instance  Eq Bool  where
    True == True = True
    False == False = True
    _ == _ = False

    x /= y = not (x == y)
```

---

### V razredu lahko definiramo tudi vrednosti.

```haskell
class  Eq a  where
    (==) :: a -> a -> Bool
    (/=) :: a -> a -> Bool

    x /= y  = not (x == y)
    x == y  = not (x /= y)
```

```haskell
instance  Eq Bool  where
    True == True = True
    False == False = True
    _ == _ = False
```

---

### Tipi razreda `Ord` podpirajo primerjavo.

#### Razredi lahko za svoje člane zahtevajo dodatne razrede.

```haskell
class  Eq a => Ord a  where
    compare   :: a -> a -> Ordering
    (<), (<=) :: a -> a -> Bool
    (>=), (>) :: a -> a -> Bool
    max, min  :: a -> a -> a

    compare x y | x == y    = EQ
                | x <= y    = LT
                | otherwise = GT

    x <= y  = compare x y /= GT
    x <  y  = compare x y == LT
    ...
```

---

### Pogoje lahko postavimo tudi pri primerkih.

```haskell
instance  Ord a => Ord [a]  where
  []     <= _      = True
  (_:_)  <= []     = False
  (x:xs) <= (y:ys) = x <= y && xs <= ys
```

```haskell
instance  Show a => Show [a]  where
  show xs = "[" ++ pomozna xs ++ "]"
    where
      pomozna [] = ""
      pomozna [x] = show x
      pomozna (x:xs) =
        show x ++ "," ++ pomozna xs
```

---

### Tipi razreda `Num` podpirajo aritmetiko.

```haskell
class  Num a  where
    (+), (-), (*) :: a -> a -> a
    negate        :: a -> a
    abs           :: a -> a
    signum        :: a -> a
    fromInteger   :: Integer -> a

    x - y         = x + negate y
    negate x      = 0 - x
```

---

class: center, middle, question

### Vektorski prostori

---

class: center, middle

## Razredi konstruktorjev tipov

---

### Kaj je tip funkcije `and`?

```haskell
λ> and [True, False, True]
False

λ> and [True, True]
True

λ> and []
True
```

<pre><code class="haskell hljs remark-code">λ> :t and
and :: <span class="spoiler"><span class="red">[Bool] -> Bool</span><br>       <span class="spoiler green">Foldable t => t Bool -> Bool</span>
</code></pre>

---

### Konstruktor `Drevo`

```haskell
data Drevo a
  = Prazno
  | Sestavljeno a (Drevo a) (Drevo a)
  deriving Show
```

```haskell
mojeDrevo =
  Sestavljeno 1
    (Sestavljeno 2
      (Sestavljeno 3 Prazno Prazno)
      Prazno
    )
    (Sestavljeno 4 Prazno Prazno)
```

---

### Razred `Functor`

```haskell
class  Functor f  where
    fmap :: (a -> b) -> f a -> f b
```

```haskell
instance  Functor Drevo  where
    fmap _ Prazno = Prazno
    fmap f (Sest x l d) =
      Sest (f x) (fmap f l) (fmap f d)
```

```haskell
λ> fmap odd (Sest 3 Prazno Prazno)
Sest True Prazno Prazno

λ> fmap odd [1, 2, 5]
[True,False,True]
```

---

### Razred `Foldable`

```haskell
class  Foldable t  where
    foldr :: (a->b->b) -> b -> t a -> b
```

```haskell
instance  Foldable Drevo  where
    foldr f z Prazno = z
    foldr f z (Sestavljeno x l d) =
      foldr f (f x (foldr f z d)) l
```

```haskell
λ> sum mojeDrevo
10

λ> 3 `elem` mojeDrevo
True
```

---

class: center, middle

### naslednjič...

# Pravilnost programov


    </textarea>
    <script src="../../js/jquery-1.11.3.min.js"></script>
    <script src="../../js/remark-latest.min.js"></script>
    <script src="https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML"></script>
    <script>
      $(document).ready(function() { 
      
      $(".spoiler").hide();
      
      $('<a class="reveal">???</a>').insertBefore('.spoiler');

      $("a.reveal").click(function(){
        $(this).next().fadeIn(500);
        $(this).hide();
      });

    }); 
        var slideshow = remark.create({
        highlightLines: true
      });
    </script>
  </body>
</html>
